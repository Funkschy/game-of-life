// SDL_Window
type Window struct {}

def (w: *Window) destroy() {
    SDL_DestroyWindow(w);
}

def (w: *Window) surface(): *Surface {
    return SDL_GetWindowSurface(w);
}

def (w: *Window) update_surface(): i32 {
    return SDL_UpdateWindowSurface(w);
}

def (w: *Window) set_title(title: string) {
    SDL_SetWindowTitle(w, title);
}

type Rect struct {
    x: i32,
    y: i32,
    w: i32,
    h: i32
}

def rect(x: i32, y: i32, w: i32, h: i32): Rect {
    return Rect { x:x, y:y, w:w, h:h };
}

def (r: Rect) is_inside_horizontal(other: Rect): bool {
    return r.x >= other.x && r.x + r.w <= other.x + other.w;
}


def (r: Rect) is_inside_vertical(other: Rect): bool {
    return r.y >= other.y && r.y + r.h <= other.y + other.h;
}

type Point struct {
    x: i32,
    y: i32
}

// SDL_Texture
type Texture struct {}

def (s: *Texture) destroy() {
    SDL_DestroyTexture(s);
}

// SDL_Surface
type Surface struct {
    flags: i32,
    format: *void,
    w: i32,
    h: i32,
    pixels: *void,
    userdata: *void,
    locked: i32,
    lock_data: *void,
    clip_rect: Rect,
    map: *void,
    refcount: i32
}

def (s: *Surface) free() {
    SDL_FreeSurface(s);
}

// SDL_Renderer
type Renderer struct {}

def create_renderer(win: *Window, index: i32, flags: i32): *Renderer {
    return SDL_CreateRenderer(win, index, flags);
}

def (r: *Renderer) destroy() {
    SDL_DestroyRenderer(r);
}

def (r: *Renderer) clear(): i32 {
    return SDL_RenderClear(r);
}

def (r: *Renderer) present() {
    SDL_RenderPresent(r);
}

def (render: *Renderer) set_draw_color(r: i32, g: i32, b: i32, a: i32): i32 {
    return SDL_SetRenderDrawColor(render, r, g, b, a);
}

def (r: *Renderer) surface_to_texture(s: *Surface): *Texture {
    return SDL_CreateTextureFromSurface(r, s);
}

def (r: *Renderer) draw_rect(rect: *Rect): i32 {
    return SDL_RenderDrawRect(r, rect);
}

def (r: *Renderer) fill_rect(rect: *Rect): i32 {
    return SDL_RenderFillRect(r, rect);
}

def (r: *Renderer) copy_ex(
    t: *Texture,
    src: *Rect,
    dest: *Rect,
    angle: f32,
    center: *Point,
    flip: RendererFlip
): i32 {
    return SDL_RenderCopyEx(r, t, src, dest, angle, center, flip);
}

type EventKind enum {
    None,
    Quit,
    Window,

    KeyDown,
    KeyUp,
    TextEditing,
    TextInput,

    MouseMotion,
    MouseButtonDown,
    MouseButtonUp,
    MouseWheel,

    JoyAxisMotion,
    JoyBallMotion,
    JoyHatMotion,
    JoyButtonDown,
    JoyButtonUp,
    JoyDeviceAdded,
    JoyDeviceRemoved
}

def check_event_kind(i: i32, first_val: i32, count: i32, start: EventKind): EventKind {
    if i >= first_val && i <= first_val + count {
        let normal = i - first_val + *(&start as *i32);
        return *(&normal as *EventKind);
    }

    return EventKind.None;
}

// SDL_Event always has a width 56 bytes
type Event struct {
    kind: EventKind,
    timestamp: i32,
    b: i64,
    c: i64,
    d: i64,
    e: i64,
    f: i64,
    g: i64
}

def (e: *Event) poll(): i32 {
    let ret = SDL_PollEvent(e);
    e.kind = translate_event_kind(e);

    return ret;
}

def translate_event_kind(e: *Event): EventKind {
    let kind_value = *(&e.kind as *i32);

    if kind_value == 256 {
        return EventKind.Quit;
    }

    if kind_value == 512 {
        return EventKind.Window;
    }

    let kind = check_event_kind(kind_value, 1024, 4, EventKind.MouseMotion);
    if kind != EventKind.None { return kind; }
    kind = check_event_kind(kind_value, 768, 4, EventKind.KeyDown);
    if kind != EventKind.None { return kind; }
    kind = check_event_kind(kind_value, 1536, 7, EventKind.JoyAxisMotion);

    return kind;
}

type KeyEvent struct {
    kind: EventKind,
    timestamp: i32,
    window_id: i32,
    padding: i32,
    scan_code: i32,
    key_code: i32
}

def (e: *Event) as_key_event(): *KeyEvent {
    return e as *KeyEvent;
}

type JoyButtonEvent struct {
    kind: EventKind,
    timestamp: i32,
    which: i32,
    button: i8,
    state: i8
}

def (e: *Event) as_joybutton_event(): *JoyButtonEvent {
    return e as *JoyButtonEvent;
}

type MouseButtonEvent struct {
    kind: EventKind,
    timestamp: i32,
    window_id: i32,
    which: i32,
    button: i8,
    state: i8,
    clicks: i8,
    x: i32,
    y: i32
}

def (e: *Event) as_mouse_button_event(): *MouseButtonEvent {
    return e as *MouseButtonEvent;
}

def (e: *MouseButtonEvent) is_left(): bool {
    return e.button == 1;
}

def (e: *MouseButtonEvent) is_right(): bool {
    return e.button == 3;
}

type MouseMotionEvent struct {
    kind: EventKind,
    timestamp: i32,
    window_id: i32,
    which: i32,
    state: i32,
    x: i32,
    y: i32,
    xrel: i32,
    yrel: i32
}

def (e: *Event) as_mouse_motion_event(): *MouseMotionEvent {
    return e as *MouseMotionEvent;
}

type JoyHatValue enum {
    Release,
    Up,
    Right,
    UpRight,
    Down,
    Filler1,
    DownRight,
    Filler2,
    Left,
    UpLeft,
    Filler3,
    Filler4,
    DownLeft
}

type JoyHatEvent struct {
    kind: EventKind,
    timestamp: i32,
    joystick: i32,
    hat: i8,
    value: i8
}

def (e: *Event) as_joyhat_event(): *JoyHatEvent {
    return e as *JoyHatEvent;
}

def (j: *JoyHatEvent) get_value(): JoyHatValue {
    return *(&j.value as *JoyHatValue);
}

type JoyAxisEvent struct {
    kind: EventKind,
    timestamp: i32,
    joystick: i32,
    axis: i8,
    padding1: i8,
    padding2: i8,
    padding3: i8,
    value: i16
}

def (e: *Event) as_joyaxis_event(): *JoyAxisEvent {
    return e as *JoyAxisEvent;
}

type Joystick struct {}

def num_joysticks(): i32 {
    return SDL_NumJoysticks();
}

def open_joystick(index: i32): *Joystick {
    return SDL_JoystickOpen(index);
}

def (js: *Joystick) close() {
    if js != null {
        SDL_JoystickClose(js);
    }
}

let sdl_init_video = 32;
let sdl_init_joystick = 512;

let sdl_window_centered = 805240832;
let sdl_window_shown = 4;

let sdl_renderer_presentvsync = 4;

def init_video(): i32 {
    return sdl_init_video;
}

def init_joystick(): i32 {
    return sdl_init_joystick;
}

def quit() {
    SDL_Quit();
}

def window_centered(): i32 {
    return sdl_window_centered;
}

def window_shown(): i32 {
    return sdl_window_shown;
}

def present_vsync(): i32 {
    return sdl_renderer_presentvsync;
}

extern def SDL_Init(flags: i32): i32;
extern def SDL_GetError(): string;
extern def SDL_CreateWindow(title: string, x: i32, y: i32, w: i32, h: i32, flags: i32): *Window;
extern def SDL_SetWindowTitle(w: *Window, title: string);
extern def SDL_PollEvent(e: *Event): i32;
extern def SDL_RenderClear(r: *Renderer): i32;
extern def SDL_CreateRenderer(window: *Window, index: i32, flags: i32): *Renderer;
extern def SDL_SetRenderDrawColor(renderer: *Renderer, r: i32, g: i32, b: i32, a: i32): i32;
extern def SDL_RenderPresent(renderer: *Renderer);
extern def SDL_RenderDrawRect(renderer: *Renderer, rect: *Rect): i32;
extern def SDL_RenderFillRect(renderer: *Renderer, rect: *Rect): i32;
extern def SDL_DestroyWindow(win: *Window);
extern def SDL_DestroyRenderer(r: *Renderer);
extern def SDL_FreeSurface(s: *Surface);
extern def SDL_DestroyTexture(s: *Texture);
extern def SDL_Quit();
extern def SDL_RWFromFile(filename: string, mod: string): *RwOps;
extern def SDL_GetWindowSurface(win: *Window): *Surface;
extern def SDL_UpperBlit(src: *Surface, src_rect: *Rect, dest: *Surface, dest_rect: *Rect): i32;
extern def SDL_UpdateWindowSurface(win: *Window): i32;
extern def SDL_CreateTextureFromSurface(r: *Renderer, s: *Surface): *Texture;
extern def SDL_NumJoysticks(): i32;
extern def SDL_JoystickOpen(index: i32): *Joystick;
extern def SDL_JoystickClose(js: *Joystick);
extern def SDL_JoystickOpened(index: i32): bool;
extern def SDL_JoystickGetHat(js: *Joystick, hat: i32): i8;
extern def SDL_GetPerformanceCounter(): i64;
extern def SDL_GetPerformanceFrequency(): i64;

def delta(last: i64): f64 {
    let last = last;
    let now = now();
    return ((now - last) as f64 * 1000.0) / (SDL_GetPerformanceFrequency() as f64);
}

def now(): i64 {
    return SDL_GetPerformanceCounter();
}

type RendererFlip enum {
    None, Horizontal, Vertical
}

extern def SDL_RenderCopyEx(
    r: *Renderer,
    t: *Texture,
    src: *Rect,
    dest: *Rect,
    angle: f32,
    center: *Point,
    flip: RendererFlip
): i32;

// SDL_RWops
type RwOps struct {}
extern def SDL_LoadBMP_RW(rwops: *RwOps, freesrc: i32): *Surface;

def load_bmp(filename: string): *Surface {
    return SDL_LoadBMP_RW(SDL_RWFromFile(filename, "rb"), 1);
}

def init(flags: i32): i32 {
    return SDL_Init(flags);
}

def get_error(): string {
    return SDL_GetError();
}

def create_window(title: string, x: i32, y: i32, w: i32, h: i32, flags: i32): *Window {
    return SDL_CreateWindow(title, x, y, w, h, flags);
}

def blit_surface(src: *Surface, src_rect: *Rect, dest: *Surface, dest_rect: *Rect): i32 {
    return SDL_UpperBlit(src, src_rect, dest, dest_rect);
}
